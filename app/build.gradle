plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    signingConfigs {
        ttnmapper {
            storeFile file('/home/jpmeijers/PhoneSurveyor/keystore.jks')
            storePassword 'ttnmapper'
            keyAlias 'ttnmapper'
            keyPassword 'ttnmapper'
        }
    }
    compileSdkVersion 31
    defaultConfig {
        applicationId "org.ttnmapper.phonesurveyor"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 34
        versionName "2021.12.17"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.ttnmapper
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '30.0.2'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation 'org.osmdroid:osmdroid-android:6.1.10'

    implementation 'com.github.hannesa2:paho.mqtt.android:3.3.3'

    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.13.0")

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Room database
    def room_version = "2.3.0" // check latest version from docs
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation "androidx.room:room-testing:$room_version"
}
